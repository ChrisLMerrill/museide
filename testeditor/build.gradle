plugins {
    id 'java-library'
    id 'maven-publish'                                                      
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'org.museautomation'
version '0.11'
                                                                            
javafx {
    version = "14.0.1"
    modules = [ 'javafx.controls' ]
}

dependencies {
    implementation 'org.museautomation:muse-core:[0.22,)'
    implementation 'org.museautomation:muse-selenium:[0.22,)'

    implementation 'org.museautomation:ide-extend:[0.10,)'
    implementation 'org.museautomation:ide-valuesource:[0.10,)'
    implementation 'net.christophermerrill:FancyFxTree:[3.6,)'
    implementation 'org.openjfx:javafx-controls:14.0.1'
    implementation 'org.controlsfx:controlsfx:11.0.1'

    testImplementation 'net.christophermerrill:TestFxUtils:[2.0,)'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:[5.0,)'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:[5.0,)'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenLocal()
    jcenter()
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    }

javadoc.options.addStringOption('Xdoclint:none', '-quiet')
javadoc.options.addStringOption('Xdoclint:none', '-html5')

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
    }

artifacts {
    archives sourcesJar
    archives javadocJar
    }

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

wrapper {
    gradleVersion = '6.3'
}
