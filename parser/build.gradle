plugins {
    id 'java-library'
    id 'maven-publish'
    id 'antlr'
}

group = 'org.museautomation'
version = 0.15

dependencies
    {
    implementation 'org.slf4j:slf4j-api:1.7.12'
    implementation "org.antlr:antlr4:4.7.2" // while the antlr step below is sufficient to compile the parser, without this compile dependency, dependent projects must also declare ANTLR

    antlr "org.antlr:antlr4:4.7.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5+'
    }

repositories
    {
    mavenCentral()
    jcenter()
    }

generateGrammarSource
    {
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/main/org/museautomation/parsing/valuesource/antlr".toString())   // update *.g files if you change this
    }

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    }

javadoc {
    options.addBooleanOption('html5', true)
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
    }

artifacts {
    archives sourcesJar
    archives javadocJar
    }

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

wrapper {
    gradleVersion = '6.3'
}
